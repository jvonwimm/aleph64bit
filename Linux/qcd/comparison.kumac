*** Begin of history file: Thu Oct  8 05:43:15 2020
* set all options to paw default settings
opt *; set *
* here set some options we want
opt nbox
SET *FON -40
SET TXFP -40
set hwid 3
set yhti 0.7

* make sure all units and histograms closed
close 0; hi/de 0

* now read the input files to compare with each other:
h/fil 1 qcdana_da_94_32bit.hbook
h/fil 2 qcdana_da_94_64bit.hbook
* output postscript file on fortran unit 99:
for/fil 99 comparison.ps
meta 99 -111

* set j[i]=1 for all existing histograms, 0 otherwise 

do i=1,830
 j[i]=0
enddo
do i=1,55
 j[i]=1
enddo
j39=0
j40=0
do i=100,180,10
 j[i]=1
enddo
j181=1
j182=1
j183=1
j200=1
j300=1
j410=1
j420=1
j430=1
j440=1
j510=1
j520=1
j530=1
j540=1
j550=1
j610=1
j620=1
j630=1
j710=1
j720=1
j810=1
j820=1
j830=1

*

* big loop to plot all the histograms according to above list
zone 1 3
do i=1,830
 k=[j[i]]
 if ([k].eq.1) then
  opt stat
  set txci 1
* plot histograms for file on unit 1 in red
  set hcol 2; h/pl //lun1/[i]
* plot histograms for file on unit 1 in blue
  set hcol 4; h/pl //lun2/[i]
* subract histogram i in unit 2 from i in unit 1
* and create a new histogram with id 999 with the result
  subtract //lun1/[i] //lun2/[i] 999
* find the minimum of the y-axis for histogram 999
  yl=$HINFO(999,'MIN')
  ymin=1.1*[yl]
* now set the minimum of y-axis of 999 for plotting purposes
  h/set/min 999 [ymin]
  opt nstat
* plot 999 in black
  set hcol 1; h/pl 999
* annotate the plots
  selnt 1
  set txci 2; itx 8.5 18.1 '32-bit f77'
  set txci 4; itx 8.5 12.1 '64-bit gfortran'
  set txci 1; itx 8.5 6.1 '32-bit minus 64-bit'
* delete histogram 999
  hi/de 999
 endif
enddo

* close fortran unit 99 (postscript file)
close 99

* use ps2pdf tool to convert to pdf format
shell ps2pdf comparison.ps
